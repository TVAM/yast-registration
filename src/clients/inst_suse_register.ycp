/**
 * File:        installation/general/inst_suse_register
 * Module:      Installation
 * Summary:     Perform Customer Center Configuration
 *              this includes (by calling suse_register):
 *                              machine registration
 *                              if needed, launch browser for user/customer registration
 *                              ZMD configuration
 * Authors:     J. Daniel Schmidt <jdsn@suse.de>
 *
 * Perform Customer Center Configuration
 *
 * $Id: inst_suse_register.ycp 1 2006-02-17 13:20:02Z jdsn $
 */

{
    textdomain "registration";

    import "FileUtils";
    import "InstURL";
    import "URL";
    import "Wizard";
    import "Popup";
    import "GetInstArgs";
    import "CustomDialogs";
    import "Directory";
    import "Mode";
    import "String";
    import "Label";
    import "Internet";
    import "Register";
    import "SourceManager";
    import "Package";
    import "PackageCallbacks";


    // this operation MUST be first and run in any case, even if registration should be skipped (FATE #302966)
    symbol confRegSrv = Register::configureRegistrationServer();
    if (confRegSrv == `conferror  || confRegSrv == `notrust  || confRegSrv == `silentskip)
    {
        y2debug("Registration can not be run due to SMT configuration error.");
        return `auto;
    }


    // no network - no suse_register
    if (!Mode::normal())
    {
        if (!Internet::suse_register)
        {
            Internet::do_you = false;
            return `auto;
        }
    }

    // error handling - skip if suseRegister is missing
    integer sr_exist = (integer) SCR::Execute(.target.bash, "test -x /usr/bin/suse_register");
    if (sr_exist != 0)
    {
        y2milestone("/usr/bin/suse_register does not exist - skipping registration");
        Internet::do_you = false;
        return `auto;
    }

    // Register::initialize() is mandatory - never remove it
    Register::initialize();

    map ui = UI::GetDisplayInfo();
    boolean textmode = (boolean) ui["TextMode"]:nil;
    boolean interactive = ! (textmode && Register::disable_w3m);

    map argmap = GetInstArgs::argmap();

    /* strings for main (wizard) layout  */

    // Translators: This is title/brand - verify how to translate it correctly
    string title_nccc = _("Novell Customer Center Configuration");

    // Translators: Module Title for the Box
    string title_box  = _("Online Update Configuration");  // (#165509)

    // alternative short title text
    string title_short = _("Registration");

    // select title to use
    //    set default title
    string title = title_nccc;

    // lets see if the control center uses the box text and no customer center desktop file is there (#294454)
    if ( (integer) SCR::Execute(.target.bash, " grep -isq \"^Name=Online Update Configuration\" /usr/share/applications/YaST2/suse_register.desktop && ! [ -e /usr/share/applications/YaST2/customer_center.desktop ] ") == 0 ? true:false )
        title = title_box;


    // Translators: limit to 2x 50 charachters - if more needed take 3x 50 chars but NOTHING more
    string nccc_top = _("Get technical support and product updates and
manage subscriptions with Novell Customer Center.");

    string nccc_configure   = _("Configure Now (Recommended)");
    string nccc_noconfigure = _("Configure Later");

    string nccc_sub_comment = _("Include for Convenience");
    string nccc_sub_hwinfo  = _("Hardware Profile");
    string nccc_sub_optional= _("Optional Information");
    string nccc_forcereg= _("Registration Code");
    string nccc_sub_showbtn = _("Details...");


    /*  strings for success popup   */
    string nccc_success_msg = _("Your configuration was successful.");
    string nccc_success_server_added = _("An update server has been added to your configuration.");
    string nccc_error_no_server_added = _("No update server could be added to your configuration.");
    /* caption for details view  */
    string success_detail_label =_("New Update Server");

   /* strings for manual interaction popup */
    string mi_required = _("Manual Interaction Required");
    string mi_needinfo = _("Needed Information");
    string mi_browser_btn = _("Continue");
    string mi_start_browser = _("A Web browser will start in which to complete
the configuration on the opened Web site.");


    string title_regularly_run = _("Regularly Synchronize with the Customer Center");

    /* string for show information popup */
    string transmit_info = _("Registration and Privacy Information");

    /*  strings for conflict popup */
    string conflict_need_info = _("The server requires additional system information. Activating 
submission of the hardware profile automatically.");

     /*  nonroot-message strings */
     string nonroot_title = _("Update Source Issues");
     string nonroot_message = _("Registering as a regular user does not include the update source
in the Online Update YaST module. If you continue and later want 
to update with Online Update, the source must be added manually.
Other tools, such as Software Updater in the panel, can still be 
used. Alternatively, cancel then register through YaST as root 
so the sources are available to all tools.");
     /* nonroot-message using repository instead of source*/
     string nonroot_message_repo = _("Registering as a regular user does not include the update repository
in the Online Update YaST module. If you continue and later want 
to update with Online Update, the repository must be added manually.
Other tools, such as Software Updater in the panel, can still be 
used. Alternatively, cancel then register through YaST as root 
so the repositories are available to all tools.");



    /* error messages  */
    string data_invalid = _("Error: Data received is invalid.");
    string retrieve_error = _("Error: Could not retrieve data.");
    string no_browser_msg = _("No Web browser available.
Run the suse_register command manually.");
    string no_product_msg = _("No product found to be registered.
You do not need to register this installation.
Please add update sources yourself.");
    string no_w3m_msg = _("Your registration requires interactive input that is not
supported in text mode. Run YaST2 in the graphical mode or
run the suse_register command manually.");

    /* help text for dialog "Novell Customer Center Configuration" */
string help_title = sformat( "<p><b>%1</b></p>", title);
string help_para1 = _("<p>
Configure your system to enable online updates by registering it with Novell.
To do this now, select <b>Configure Now</b>. Delay the registration with
<b>Configure Later</b>.
</p>");

string help_para2 = _("<p>
To simplify the registration process, include information from your system
with <b>Optional Information</b> and <b>Hardware Profile</b>. 
<b>Details</b> shows the maximum amount of information that can be involved
 in your registration. To obtain this information, it contacts the Novell
server to query what information is needed for your product. Only the identity
of the installed product is sent in this initial exchange.
</p>");

string help_para3 = _("<p>
If you purchased your copy of this product, enable <b>Registration Code</b>
so you are prompted for your product code. 
This registers you for the installation support included with your product.
</p>");

string help_para4 = _("<p>
No information is passed to anyone outside Novell. The data is used for
statistical purposes and to enhance your convenience regarding driver support
and your Web account. Find a detailed privacy policy in <b>Details</b>. View
the transmitted information in the log file <tt>~/.suse_register.log</tt>.
</p>");

string help_para5 = _("<p>
<b>Regularly Synchronize with the Customer Center</b> checks that your update 
sources are still valid and adds any new ones that may be available.
It additionally sends any modifications to your included data to Novell, such 
as hardware information if <b>Hardware Information</b> is activated.
This option does not remove any sources added manually.
</p>");

string help_para5_repo = _("<p>
<b>Regularly Synchronize with the Customer Center</b> checks that your update 
repositories are still valid and adds any new ones that may be available.
It additionally sends any modifications to your included data to Novell, such 
as hardware information if <b>Hardware Information</b> is activated.
This option does not remove any repositories added manually.
</p>");

string help = help_title + help_para1 + help_para2 + (Register::display_forcereg ? help_para3:"") + help_para4;


    /*  further strings   */
    string checking         = _("Checking...");
    string error            = _("Error");
    string server_error     = _("An error occurred while connecting to the server.");
    string details          = _("Details...");
    string error_msg        = _("Error");
    string starting_browser = _("Starting browser...");

    /* other string variables */
    map<string, string> information_data = nil;
    string information_text = "";


    // default is true, see statement in layout term
    boolean configure_status = true;

    /* vv   MAIN (WIZARD) LAYOUT  vv  */
    term sr_layout=nil;

    sr_layout = `HVSquash(
    `VBox(
        `Label( nccc_top ),
        `VSpacing(0.5),
        `Frame( title,
            `RadioButtonGroup( `id(`sr_perform),
            `MarginBox( 2, 0.5,
            `VBox(
                `Left ( `RadioButton(`id(`noconfigure), `opt(`notify), nccc_noconfigure )),
                `Left ( `RadioButton(`id(`configure),   `opt(`notify), nccc_configure, true )),
                `Left ( `id(`includeinfo),
                `HBox( `HSpacing( 3.0 ),
                       `VBox (
                           `VSpacing(0.5),
                           `Left( `Label( nccc_sub_comment ) ),
                           `Left( `CheckBox(`id(`hwinfo),  `opt(`notify), nccc_sub_hwinfo,  Register::submit_hwdata ) ),
                           `Left( `CheckBox(`id(`optional), `opt(`notify), nccc_sub_optional, Register::submit_optional ) ),
                            Register::display_forcereg ? `Left( `CheckBox(`id(`forcereg), `opt(`notify), nccc_forcereg, false ) ):`Empty(),
                           `VSpacing(0.5),
                            // active in SLE products only - merged here for consistency
                            //`VSpacing(0.5),
                            //`Left( `CheckBox(`id(`regularly_run), `opt(`notify), title_regularly_run, Register::register_regularly ) ),
                           `Right( `PushButton(`id(`showinfo), nccc_sub_showbtn ) )
                        )
                    )
                )
            ))
        ))
    ));



    term contents = `VBox ( `VSpacing (0.5), sr_layout, `VSpacing (0.5));
    /* ^^       END MAIN LAYOUT     ^^  */



    /* vv      SHOW INFO POPUP     vv  */
    term showinformation = `HBox( `HSpacing(0.5),
                                  `MinSize(  75, 22 ,`VBox(
                                     `Label( transmit_info ),
                                     `RichText(`id(`information_text), information_text ),
                                     `PushButton (`id (`info_close), Label::CloseButton () )  )
                                   ),
                                  `HSpacing(0.5)
    );
    /* ^^      END  SHOW INFO       ^^  */





    /* vv  MANUAL INTERACTION POPUP vv  */
    term manual_interaction = //`HBox(
                                   `MinSize( 70, 25, `HBox( `VBox(
                                           `Left( `Label( `opt(`boldFont), mi_required ) )  ,
                                           `VSpacing(0.5) ,
                                           `Left( `Label( mi_start_browser  ) ) ,
                                           `Left( `Label( mi_needinfo ) )  ,
                                                  `RichText(`id(`needinfo), ""  ),
                                           `HBox(
                                                  `PushButton(`id(`start_browser), mi_browser_btn )  ,
                                                  `HSpacing( 3),
                                                  `PushButton(`id(`cancel),  Label::CancelButton()  )
                                           )
                                   ))
    );
    /* ^^  END MANUAL INTERACTION  ^^  */



    /* vv      CCC CONFLICT POPUP      vv  */
    term ccc_conflict=
    `VBox(
          `Left( `Label( `opt(`boldFont), title ) ),
          `VSpacing( 0.5 ),
          `Left( `Label( conflict_need_info ) ),
          `PushButton(`id(`ok), Label::OKButton())
    );
    /* ^^       END CCC CONFLICT       ^^  */


    /* vv      CCC ERROR POPUP      vv  */
    term error_message=
    `VBox(
           `Left( `Label( `opt(`boldFont), error ) ),
           `VSpacing( 0.5 ),
           `Left( `Label( `id(`err_description), server_error ) ),
           `HBox(
                  `HWeight(1, `PushButton(`id(`back), Label::BackButton()  ) ),
                  `HSpacing( 3 ),
                   // reactivated skipping the registration (#240174)
                  `HWeight(1, `PushButton(`id(`skip), Label::SkipButton()  ) ),
                  `HSpacing( 3 ),
                  `HWeight(1, `PushButton(`id(`err_detail), details ) )
                )
    );
    /* ^^      END CCC ERROR        ^^  */


    /* vv      CCC ERROR POPUP      vv  */
    term error_message_details=
    `MinSize( 60, 20,  `VBox(
           `Left( `Label( `opt(`boldFont), error_msg ) ),
           `VSpacing( 0.5 ),
           `RichText(`id(`errordetail), "" ),
           `PushButton(`id(`ok), Label::OKButton())
    ));
    /* ^^      END CCC ERROR        ^^  */


    /* vv      CCC ERROR POPUP PLAINTEXT     vv  */
    term error_message_details_pt=
    `MinSize( 60, 20,  `VBox(
           `Left( `Label( `opt(`boldFont), error_msg ) ),
           `VSpacing( 0.5 ),
           `RichText(`id(`errordetail), `opt(`plainText), "" ),
           `PushButton(`id(`ok), Label::OKButton())
    ));
     /* ^^      END CCC ERROR PLAINTEXT       ^^  */



    /* vv       SUCCESS MESSAGE    vv  */
    term nccc_success=
    `VBox(
           `Left( `Label( `opt(`boldFont), title ) ),
           `VSpacing( 0.5 ),
           `Left( `Label( nccc_success_msg ) ),
           `Left( `Label( nccc_success_server_added ) ),
           `HBox(
                  `HWeight(1, `PushButton(`id(`ok), Label::OKButton()  ) ),
                  `HSpacing( 3 ),
                  `HWeight(1, `PushButton(`id(`success_detail), details ) )
           )
    );
    /* ^^   SUCCESS MESSAGE END     ^^  */



    /* vv    SUCCESS DETAILS     vv  */
    term nccc_success_detail=
    `MinSize( 60, 7,  `VBox(
           `Left( `Label( `opt(`boldFont), success_detail_label ) ),
           `VSpacing( 0.5 ),
           `RichText(`id(`success_detail_richtext), `opt(`plainText) , "" ),
           `PushButton(`id(`ok), Label::OKButton())
    ));
    /* ^^    SUCCESS DETAILS END    ^^  */


/* ---------------------------------- LOCAL FUNCTIONS ------------------------------------------------------------  */


boolean report_error()
{
    // function to display an error message
    // and offer a detailled view of the error message

    UI::OpenDialog(error_message);
    UI::SetFocus (`id (`skip ));
    symbol retry = nil;

    repeat
    {
        retry = (symbol) UI::UserInput();

        if ( retry == `skip ) configure_status = false;
        else if (retry == `err_detail)
        {
            // switch to plaintext if error output is one or two lines (#239570)
            string error_msg_details = (string)information_data["stderr"]:"no error message available";
            if ( contains( [0,1], size(splitstring(error_msg_details,"\n")) ) )
               UI::OpenDialog(error_message_details);
            else
               UI::OpenDialog(error_message_details_pt);
            UI::ChangeWidget( `id(`errordetail), `Value, error_msg_details);
            UI::UserInput();
            UI::CloseDialog();
        }
    } until ( retry != `err_detail );

    UI::CloseDialog();
}


string su_exec(string user, string group, string command)
{   // (#167255)
    string exec = sformat("
#!/bin/bash -x

user=%1
group=%2
cmd=\"%3\"

fakehome=/var/lib/YaST2/$user-fakehome
umask 0077

XA=/root/.xauth
mkdir -p $XA
grep ^$user$ $XA/export >/dev/null 2>&1  || echo $user >> $XA/export

mkdir -p $fakehome
chmod 700 $fakehome
tmp=$(mktemp $fakehome/.Xauthority.XXXXXX) || exit 1
chmod 600 $tmp
chown $user:$group $tmp $fakehome
trap \"rm -rf $fakehome\" EXIT INT HUP TERM

", user, group, command );


    // screen jail can be removed - no longer used (#367719)
    // create a script to run a system call as different user
    // thanks to werner (script)
    // unset DESKTOP_SESSION : (#207332)
    if (!textmode)
    {    exec = exec +
"
if test \"${DISPLAY%:*}\" = \"localhost\" ; then
    disp=$(hostname -s)/unix:${DISPLAY#*:}
else
    disp=\"${DISPLAY}\"
fi

unset DESKTOP_SESSION

: ${XAUTHORITY:=$HOME/.Xauthority}
if test ! -e $XAUTHORITY ; then
    su -s /bin/bash -- $user -c \"cd; $cmd\"
    exit 0
fi
exec 4< ${XAUTHORITY}
su -s /bin/bash -- $user -c \"xauth -qif <(cat 0<&4) extract - $disp | xauth -qf $tmp merge -\"
exec 4<&-

su -s /bin/bash -- $user -c \"cd; XAUTHORITY=$tmp $cmd\"

exit 0";
    }
    else
    {
        // screen jail can be removed - no longer used (#367719)
        exec = exec + "su -s /bin/bash -- $user -c \"cd; $cmd\"";
    }

    y2milestone("using su_exec to launch browser");

    return exec;
}



string browser_command(string url)
{
    // create the command string to launch a browser
    string bcmd="/bin/false";
    boolean cmd_ok = false;

    if ( textmode )
    {
        cmd_ok = (integer) SCR::Execute(.target.bash, "test -x /usr/bin/w3m") == 0 ? true:false;
        // screen is no longer needed as there is UI::RunInTerminal for textmode programs now (#367719)
        bcmd = " w3m ";
    }
    else
    {
        // check for and install mozilla-xulrunner if needed (#175166), modified due to (#304310)
        string required_package = "mozilla-xulrunner181";
        if (!Package::Installed(required_package))
        {
            Package::InstallAllMsg([required_package], nil);
        }
        cmd_ok = (integer) SCR::Execute(.target.bash, "test -x /usr/bin/xulrunner") == 0 ? true:false;
        bcmd = " MOZ_DISABLE_PANGO=1 /usr/bin/xulrunner /usr/share/YaST2/yastbrowser/application.ini -url ";
    }

    if (! cmd_ok) return "nobrowser";

    // add url to browser command
    bcmd =  bcmd + "'" + url + "'";

    if (Register::use_proxy)
    {
        // (#165891) - (#208651) fixed in Register.ycp
        bcmd = " http_proxy='"  + Register::http_proxy  + "' " + bcmd;
        bcmd = " https_proxy='" + Register::https_proxy + "' " + bcmd;
    }

    // add su call to not run a browser as root during installation
    bcmd = su_exec("suse-ncc", "suse-ncc", bcmd );

    return bcmd;
}


boolean launchBrowser(string url)
{
    // check for valid url
    map parsed = URL::Parse(url);
    if ( parsed == $[]) return false;
    // no rebuild of the url - parser seems to only parse one parameter in URL

    string uri = String::FirstChunk(url, "\n");  // (#167225)
    // get command to run correct browser and execute it
    string command = browser_command(uri);
    if (command == "nobrowser") return false;

    y2milestone("launching browser: %1", command);

    if (textmode)
    {
        UI::RunInTerminal(command);
    }
    else
    {
        SCR::Execute(.target.bash, command );
    }

    UI::RedrawScreen();

    return true;
}


/* ----------------------------------- END FUNCTIONS -------------------------------------------------------------  */


    // check if we are in installation workflow or running independently
    if (Mode::normal()) Wizard::CreateDialog();

    boolean enable_back = GetInstArgs::enable_back();
    // we always need the next button
    Wizard::SetContents (title, contents, help, enable_back, true);
    Wizard::SetTitleIcon ("yast-product-registration");  // (#211552)

    //y2debug ("%1", UI::DumpWidgetTree());


    // check if we are running as nonroot
    if ( !Register::iamroot )
    {    // do not run as non-root: #170736
        return `auto;
        // SL (box) has the following warning
        //if ( !Popup::ContinueCancelHeadline( nonroot_title, nonroot_message ) )
        //    return `cancel;
    }


    boolean loopend = false;
    any ret = nil;


/* -------------------------------- PROGRAM LOGIC START ----------------------------------------------------------- */

    // #170113, the lock is needed around everything that may end up in ZMD
    SourceManager::Lock ();

    repeat {
        ret = Wizard::UserInput();

        if (ret == `abort)
        {
            if (Mode::normal()) break;
            if (Popup::ConfirmAbort (`incomplete))  break;
        }
        else if (ret == `help)
        {
            Wizard::ShowHelp (help);
        }
        else if (ret == `configure || ret == `noconfigure)
        {
            configure_status = (boolean) UI::QueryWidget(`id(`configure),  `Value);
        }
        else if (ret == `hwinfo || ret == `optional || ret == `forcereg || ret == `regularly_run )
        {
            Register::submit_hwdata  = (boolean) UI::QueryWidget(`id(`hwinfo),  `Value);
            Register::submit_optional= (boolean) UI::QueryWidget(`id(`optional), `Value);
            Register::force_registration = (boolean) UI::QueryWidget(`id(`forcereg), `Value);
            Register::register_regularly = (boolean) UI::QueryWidget(`id(`regularly_run), `Value);
        }
        else if (ret == `showinfo)
        {
            information_data = Register::suseRegister(`p);

            if ( information_data["exit"]:"99" == "0" )
            {
                information_text = information_data["stdout"]:"";
                UI::OpenDialog( showinformation );
                UI::ChangeWidget(`id(`information_text), `Value, information_text );
                any info_ret = nil;

                repeat
                {
                    info_ret = UI::UserInput();
                    if ( is(info_ret, string) ) launchBrowser( (string) info_ret);

                } until ( info_ret == `info_close );

                UI::CloseDialog();
            }
            else
            {
                if (! ( information_data["exit"]:"99"        == "199"     &&
                        information_data["stdout"]:"aborted" == "aborted" &&
                        information_data["stderr"]:"aborted" == "aborted"    ) )
                report_error();
            }

        }
        else if (ret == `next)
        {
            if ( configure_status == true )
            {
                // remove the zmd flag file (requested by mvidner)
                SCR::Execute(.target.bash, " rm -f /var/lib/zypp/zmd_updated_the_sources " );

                /* run suse_register to see if we need manual interaction */
                information_data = Register::suseRegister(nil);

                // error code 1: needinfo
                // manual interaction is requiered
                // this case MUST be first
                //  ... because during manual interaction further suse_register calls change the error code
                if (information_data["exit"]:"99" == "1" && interactive)
                {
                    UI::OpenDialog( manual_interaction );
                    UI::SetFocus (`id (`start_browser ));
                    UI::ChangeWidget( `id(`needinfo), `Value, information_data["stderr"]:data_invalid );
                    any mi_ret =nil;
                    boolean mi_loopend = false;
                    boolean recheck = true;
                    boolean browserrun = true;

                    repeat
                    {
                        recheck = true;
                        mi_ret = UI::UserInput();
                        if ( mi_ret == `start_browser  )
                        {
                            // now we launch the browser
                            UI::ChangeWidget( `id(`needinfo), `Value, starting_browser );
                            browserrun = launchBrowser(information_data["stdout"]:"http://www.opensuse.org");
                            // deactivate force_registration after each suse_register call (#bugNo.)
                            Register::force_registration = false;

                        }
                        else if ( mi_ret == `cancel  )
                        {
                            recheck = false;
                            mi_loopend = true;
                        }
                        else if ( is(mi_ret, string) )
                        {
                            // launch browser
                            browserrun = launchBrowser( (string) mi_ret);
                            recheck = false;
                        }


                        if ( recheck  && browserrun )
                        {
                            // show the user, that we are doing something
                            UI::ChangeWidget( `id(`needinfo), `Value, checking );

                            information_data = Register::suseRegister(nil);

                            if (information_data["exit"]:"99" == "0" || information_data["exit"]:"99" == "3" )
                            {
                                // error 0: everything is done, quit
                                // error 3: madatory data conflict - handle outside of manual interaction
                                mi_loopend = true;
                            }
                            else if (information_data["exit"]:"99" == "1")
                            {
                                // still needinfo, change displayed information and stay in manual interaction
                                UI::ChangeWidget( `id(`needinfo), `Value, information_data["stderr"]:retrieve_error );
                            }
                            else
                            {
                                // unknown error, let the user find an exit :)
                                mi_loopend = true;
                            }
                        }


                        if (! browserrun )
                        {
                            information_data = $["exit":"198", "stdout":"", "stderr":""  ];
                            mi_loopend = true;
                        }

                    } until ( mi_loopend );

                    // we are done with manual interaction
                    UI::CloseDialog();

                    if (mi_ret == `cancel) configure_status = false;
                }
                else if (information_data["exit"]:"99" == "1" && ! interactive)
                {
                    // w3m disabled - show message
                    Popup::Message(no_w3m_msg);
                    loopend = true;
                    ret = `skip;
                }

                // during error code 1 information_data may be changed by a suse_register call
                // no NO MORE suse_register calls below this line !!


                /*  handle error codes from FIRST suse_register call AND from manual interaction  */
                if (information_data["exit"]:"99" == "3")
                {
                    // error code 3 means:
                    // conflict between transmitted data and data to be transmit according to customer contract
                    // hwconfig is needed
                    UI::OpenDialog(ccc_conflict);
                    UI::SetFocus (`id (`ok));
                    UI::UserInput();
                    UI::CloseDialog();
                    Register::submit_hwdata = true;
                }

                // error code 0 means: everything is OK
                // no more interaction requiered - ZMD is configuered by suse_register
                if (information_data["exit"]:"99" == "0")
                {
                    // add update source
                    UI::OpenDialog(`VBox(`Label(_("Setting up online update source..."))));
                    list<string> added = Register::add_update_sources();
		    // #186978
		    added = maplist (string u, added, ``(
					 InstURL::HidePassword (u) ));
                    UI::CloseDialog();

                    // and then show success message
                    symbol sret=nil;

                    // (#261239) show success popup only on success else error message
                    if (added != nil && size(added) != 0)
                    {
                        UI::OpenDialog( nccc_success );
                        repeat
                        {
                            sret = (symbol) UI::UserInput();
                            if (sret == `success_detail)
                            {
			        string text = mergestring (added, "\n");
                                // Show the URLs that were added as update sources.
			        // Not STDERR of suse_register output, we may have
			        // declined some unsigned ones, #180820#c26.
                                UI::OpenDialog( nccc_success_detail );
                                UI::SetFocus(`id(`ok));
                                UI::ChangeWidget(`id(`success_detail_richtext), `Value, text );
                                UI::UserInput();
                                UI::CloseDialog();
                            }
                        } until (sret == `ok );
                        UI::CloseDialog();
                    }
                    else
                    {
                        Popup::Error(nccc_error_no_server_added);
                    }

                    // we are done, end of loop
                    loopend = true;
                }




                //  show a message when there are no products to register
                if (information_data["exit"]:"99" == "101")
                {
                    Popup::Message(no_product_msg);
                    loopend = true;
                }



                // error handling - no browser available for interactive mode
                if (information_data["exit"]:"99" == "198")
                {
                    Popup::Message(no_browser_msg);
                    loopend = true;
                }


                // handle any other error codes
                if (
                     (
                       information_data["exit"]:"" != "0" &&
                       information_data["exit"]:"" != "1" &&
                       information_data["exit"]:"" != "3" &&
                       information_data["exit"]:"" != "101" &&
                       information_data["exit"]:"" != "198"
                     ) &&
                    !(
                       // if return value says that suse_register was aborted by user
                       information_data["exit"]:"99"        == "199"     &&
                       information_data["stdout"]:"aborted" == "aborted" &&
                       information_data["stderr"]:"aborted" == "aborted"
                     )
                   )
                {
                    // display error message
                    report_error();
                }

            }
            else
            {
                // skipping - no online update!!
                loopend = true;
                ret = `skip;
            }
        }

        // update main widget settings - they may have changed
        if (configure_status) UI::ChangeWidget(`id(`configure), `Value, true);
        else UI::ChangeWidget(`id(`noconfigure), `Value, true);

        // gray out if later is selected (#178042)
        UI::ChangeWidget(`id(`includeinfo), `Enabled, configure_status );

        UI::ChangeWidget(`id(`hwinfo), `Value, Register::submit_hwdata);
        // (#165841)
        if (Register::display_forcereg) UI::ChangeWidget( `id(`forcereg ), `Value, Register::force_registration );

    } until ( loopend || ret == `back );

    SourceManager::Unlock ();

    if (Mode::normal())
    {
	// #172665
	Pkg::SourceFinishAll ();
	Pkg::TargetFinish ();
	Wizard::CloseDialog();
    }
    else
    {
        if (ret == `skip)
        {
            // skipping suse register - no online update
            Internet::do_you = false;
            // disable regular registrations when registration was skipped initially (#366687)
            Register::register_regularly = false;
            ret = `next;
        }
        else
        {
            // ok we can do online update
            Internet::do_you = true;
        }
    }

    // always return a proper return value
    if (! contains([`next, `abort, `back], (symbol)ret ))
    { ret = `next; }

    // Register::finish mandatory as well - do not remove (#366687)
    Register::finish();

    return (symbol)ret;
}
