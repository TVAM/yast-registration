/**
 * File:        online_update_configuration
 * Module:      Registration
 * Summary:     Configure Online Update
 * Authors:     J. Daniel Schmidt <jdsn@suse.de>
 *
 * Configure Online Update Settings
 *
 * $Id: online_update_configuration.ycp 1 2008-09-10 13:20:02Z jdsn $
 */

{
    textdomain "registration";

    import "OnlineUpdateConfiguration";
    import "Wizard";
    import "Popup";
    import "Label";
    import "URL";
    import "Mode";
    import "SourceManager";
    import "PackageCallbacks";
    import "CommandLine";
    import "Installation";

    include "registration/OUCDialogs.ycp";

    // support basic command-line output (bnc#439050)
    list wfm_args = WFM::Args();
    y2milestone ("ARGS: %1", wfm_args);
    if ( (size (wfm_args) > 0)  &&
         (contains (wfm_args, "help") || contains (wfm_args, "longhelp") || contains (wfm_args, "xmlhelp") ) )
    {
        string cmdhelp = _("Online Update Configuration Module Help");
        Mode::SetUI ("commandline");
        // TRANSLATORS: commandline help
        CommandLine::Run($["id"      : "online_update_configuration",
                           "help"    : cmdhelp
                        ]);
        y2milestone("Online Update Configuration was called with help parameter.");
        return `auto;
    }

    map ui = UI::GetDisplayInfo();
    boolean textmode = (boolean) ui["TextMode"]:false;

    string help = getOUCHelp(`default);
    term contents = getOUCDialog(`default);

/* ---------------------------------------------------------------------------------------------------------------  */

    Pkg::SourceStartManager(true);
    string targetRootDir = Mode::normal() ? "/":Installation::destdir;
    Pkg::TargetInit(targetRootDir, false);  // (bnc#449844)

    // check if we are in installation workflow or running independently -  use OKDialog (bnc#440568)
    if (Mode::normal()) Wizard::OpenOKDialog();

    // we always need the next button and never the back button
    Wizard::SetContents (moduleTitle, contents, help, false, true);
    
    if( Mode::normal() )
	Wizard::SetTitleIcon ("online_update_configuration");
    else
	Wizard::SetTitleIcon ("yast-online_update");

/* -------------------------------- PROGRAM LOGIC START ----------------------------------------------------------- */

    OnlineUpdateConfiguration::Read();

//    if (false) // for testing only
//    {
//        OnlineUpdateConfiguration::currentUpdateRepo = "";
//    }

    string replaceUpdateRepoString = OnlineUpdateConfiguration::currentUpdateRepo;


    if ( OnlineUpdateConfiguration::compareUpdateURLs( OnlineUpdateConfiguration::currentUpdateRepo, 
                                                       OnlineUpdateConfiguration::defaultUpdateRepo, false ) )
    {// in this case - we have default update repo installed
        replaceUpdateRepoString = OnlineUpdateConfiguration::currentUpdateRepo + "   " + defaultMark;
        UI::ChangeWidget(`id(`restoreDefault), `Enabled, false);
    }
    else
    { // in this case - custom or no update repo
        if ( replaceUpdateRepoString == ""  ) replaceUpdateRepoString = noRepo;

        boolean hasUpRepo = (OnlineUpdateConfiguration::defaultUpdateRepo != "" && OnlineUpdateConfiguration::defaultUpdateRepo != nil );
        UI::ChangeWidget(`id(`restoreDefault), `Enabled, hasUpRepo);
    }

    // write data to the UI
    UI::ChangeWidget(`id(`currentRepoURL),         `Value, replaceUpdateRepoString);
    UI::ChangeWidget(`id(`automaticOnlineUpdate),  `Value, OnlineUpdateConfiguration::enableAOU);
    UI::ChangeWidget(`id(`updateInterval),         `Value, OnlineUpdateConfiguration::updateInterval);
    UI::ChangeWidget(`id(`skipInteractivePatches), `Value, OnlineUpdateConfiguration::skipInteractivePatches);
    UI::ChangeWidget(`id(`autoAgreeWithLicenses),  `Value, OnlineUpdateConfiguration::autoAgreeWithLicenses);


    UI::RecalcLayout();
    //UI::RedrawScreen();

    any ret=`auto;

    repeat
    {
        ret = Wizard::UserInput();

        if ( ret == `next || ret == `ok )
        {
            OnlineUpdateConfiguration::updateInterval         = (symbol)  UI::QueryWidget(`id(`updateInterval),         `Value );
            OnlineUpdateConfiguration::skipInteractivePatches = (boolean) UI::QueryWidget(`id(`skipInteractivePatches), `Value );
            OnlineUpdateConfiguration::autoAgreeWithLicenses  = (boolean) UI::QueryWidget(`id(`autoAgreeWithLicenses),  `Value );
            OnlineUpdateConfiguration::enableAOU              = (boolean) UI::QueryWidget(`id(`automaticOnlineUpdate),  `Value );

            y2milestone("Writing online update configuration settings.");
            OnlineUpdateConfiguration::Write();
            ret = `next;
        }

        if ( ret == `restoreDefault )
        {
            if ( OnlineUpdateConfiguration::defaultUpdateRepo == nil  ||
                 OnlineUpdateConfiguration::defaultUpdateRepo == ""      )
            {
                y2milestone("No default update repo could be found in the products metadata.");

                if ( OnlineUpdateConfiguration::defaultRegistrationURL == nil ||
                     OnlineUpdateConfiguration::defaultRegistrationURL == ""    )
                {
                    y2error("No registration server set in product metadata. No update server can be setup automatically.");
                }
                else
                {
                    y2milestone("Registration is needed to get an update source.");

                    if ( Popup::YesNo( needToRegister + "\n\n" + runRegistrationNow ) )
                    {
                        y2milestone("User wants to run the registration in order to setup the default update repository.");
                        // trigger registration
                        ret = `register;
                    }
                    else
                    {
                        y2milestone("User selected not to run the registration in order to setup the default update repository.");
                    }
                }

            }
            else
            {
                y2milestone("User selected to set the default update repository: %1", OnlineUpdateConfiguration::defaultUpdateRepo);
                OnlineUpdateConfiguration::setUpdateRepo(OnlineUpdateConfiguration::defaultUpdateRepo);
            }
        }

        if (ret == `repoManager)
        {
            WFM::call("inst_source");
        }

        if (ret == `smolt)
        {
            WFM::call("system-profile");
        }

        if (ret == `register)
        {
            if ( WFM::ClientExists("inst_suse_register") )
            {
                WFM::call("inst_suse_register");
            }
            else
            {
                Popup::Error( _("The registration module is not available.") + "\n" + _("Please install yast2-registration and try again.") );
            }
        }


        // update values in UI
        // after a registration call refetch the current update repo url
        if ( ret == `restoreDefault || ret == `register )
        {
            y2milestone("Refetching current updateRepoURL.");
            replaceUpdateRepoString = OnlineUpdateConfiguration::fetchCurrentUpdateRepoURL();

            string mark = "";
            if ( OnlineUpdateConfiguration::compareUpdateURLs( replaceUpdateRepoString,
                                                               OnlineUpdateConfiguration::defaultUpdateRepo, false) )
            {
                mark = "   " + defaultMark;
                UI::ChangeWidget(`id(`restoreDefault), `Enabled, false);
            }
            y2milestone("Current updateRepoURL is: %1", replaceUpdateRepoString);
            UI::ChangeWidget(`id(`currentRepoURL), `Value,  replaceUpdateRepoString + mark);

            UI::RecalcLayout();
        }

        if ( ret == `back || ret == `abort || ret == `cancel )
        {
            if ( Popup::ReallyAbort(true) )
            {
                break;
            }
            else
            {
                ret = `continue;
            }
        }

    } until (ret == `ok || ret == `next || ret == `abort || ret == `cancel || ret == `back);

    Pkg::SourceFinishAll();

    if (! is(ret, symbol))  ret = `next;

    Wizard::CloseDialog();
    return (symbol)ret;

}
